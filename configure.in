AC_PREREQ([2.68])
AC_INIT([sonarea], [0.2], [thaytan@noraisin.net])

AM_INIT_AUTOMAKE([-Wno-portability 1.11 no-dist-gzip dist-bzip2 dist-xz tar-ustar])

AM_SILENT_RULES([yes])

AM_MAINTAINER_MODE([enable])

AC_CONFIG_SRCDIR([src/main.c])
AM_CONFIG_HEADER([config.h])

AC_CANONICAL_HOST

dnl Find C compiler with C99
AC_PROG_CC
AC_PROG_CC_STDC

LT_INIT

PKG_PROG_PKG_CONFIG([0.22])
PKG_CHECK_MODULES(SNRA_COMMON, [gobject-2.0 glib-2.0 avahi-client avahi-glib >= 0.6.30 json-glib-1.0 libsoup-2.4])

AC_MSG_NOTICE([Checking for GStreamer 1.0])
PKG_CHECK_MODULES(GST_1_0, [gstreamer-1.0 gstreamer-net-1.0], [HAVE_GST_1_0=yes], [HAVE_GST_1_0=no])
if test "x$HAVE_GST_1_0" = "xyes"; then
  GST_CFLAGS="$GST_1_0_CFLAGS -DGST_USE_UNSTABLE_API"
  GST_LIBS="$GST_1_0_LIBS"
  PKG_CHECK_MODULES(GST_RTSP, [gstreamer-rtsp-server-1.0], [HAVE_GST_RTSP=yes], [HAVE_GST_RTSP=no])
else
  AC_MSG_NOTICE([Checking for GStreamer 0.10])
  PKG_CHECK_MODULES(GST_0_10, [gstreamer-0.10 gstreamer-net-0.10])
  GST_CFLAGS="$GST_0_10_CFLAGS"
  GST_LIBS="$GST_0_10_LIBS"
  PKG_CHECK_MODULES(GST_RTSP, [gst-rtsp-server-0.10], [HAVE_GST_RTSP=yes], [HAVE_GST_RTSP=no])
fi

if test "x$HAVE_GST_RTSP" = "xyes"; then
  AC_DEFINE([HAVE_GST_RTSP], 1, [Define if gstreamer-rtsp-server is available])
fi

AC_SUBST([GST_CFLAGS])
AC_SUBST([GST_LIBS])

EXTRA_CFLAGS="-Wall -Wextra"
AC_SUBST([EXTRA_CFLAGS])

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  src/client/Makefile
  src/daemon/Makefile
])
AC_OUTPUT
